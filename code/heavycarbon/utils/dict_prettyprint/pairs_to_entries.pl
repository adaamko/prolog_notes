:- module(heavycarbon_utils_dict_prettyprint_pairs_to_entries,
          [
          pairs_to_entries/3 
          ]).

% ----------

/** <module> dict prettyprinter helper predicates

Handle transformation of an SWI-Prolog dict into a set of "Entries"
(which are compound terms holding strings for the single key and the 
possibly multi-line value) according to settings in "SettingsDict".

The predicate pairs_to_entries/3 may call a topmost predicate for
dict prettyprinting recursively, when it encounters a subdict. 
This is why this module loads =|dict_prettyprint.pl|=.

If you need to load this module manually, run:

```
?- use_module(library('heavycarbon/utils/dict_prettyprint/pairs_to_entries.pl')).
```

@license [Zero-Clause BSD / Free Public License 1.0.0 (0BSD)](https://opensource.org/licenses/0BSD)
@author David Tonhofer (ronerycoder@gluino.name)

*/

:- use_module(library('heavycarbon/support/safe_format.pl')).
:- use_module(library('heavycarbon/utils/dict_prettyprint.pl')).
:- use_module(library('heavycarbon/utils/dict_prettyprint/settings.pl')). 
:- use_module(library('heavycarbon/utils/dict_prettyprint/topmost.pl')). % contains the predicate called on recursion

% ----------

%! pairs_to_entries(+Pairs,+SettingsDict,-Entries)
%
% Iterates over the pairs Pairs obtained from the dict that shall be printed
% and transforms them into Entries, a list of structures described below.
%
% Entries contains the transformed pairs in the order in which they appear
% in Pairs.
%
% Each element in Entries (an "entry") is a pair =|KeyString-Lineified|= where:
% 
%    - =|KeyString|= is a string generated from the dict key (which is always 
%      an atom or an integer) The caller will want to justify this string when
%      creating the final line of text.
%    - =|Lineified|= is a compound term that is one of:
%       - ``mono(String)`` : =|String|= is a single string generated by formatting 
%         the value; it can be printed "as is" after left/right/center 
%         justification.
%       - ``poly(List)`` : =|List|= is a list of string generated by formatting a 
%         subdict; all the strings in =|List|= must be integrated into the lines
%         generated for the current dict.

pairs_to_entries(Pairs,SettingsDict,Entries) :-
   maplist(mpl_p2e(SettingsDict),Pairs,Entries).

mpl_p2e(SettingsDict,Key-Value,KeyString-Lineified) :-
   stringify_key(Key,SettingsDict,KeyString),        % straightforward
   lineify_value(Value,SettingsDict,Lineified).      % may cause recursion if the value is a dict
 
% ---
% String generation for a dict key. No string justification occurs yet.
% "SettingsDict" is currently passed but not yet needed.
% ---

stringify_key(Key,_SettingsDict,String) :-
   (atom(Key)
    -> F="~a"
    ;  integer(Key)
    -> F="~d"
    ;  domain_error([atom,integer],Key)), % never happens unless the implementation of dict changes
   format(string(String),F,[Key]).

% ----------

% String or multi-string generation for a dict value.

% "Value" is itself a dict.

lineify_value(Value,SettingsDict,poly(Lines)) :-
   is_dict(Value),
   !,
   get_dict(depth,SettingsDict,Depth),                 % depth (the depth of the recursion) must exist
   DepthP is Depth+1,
   put_dict(depth,SettingsDict,DepthP,SettingsDict2),
   pp_if_shallow_enough(Value,SettingsDict2,Lines).    % **** recursive call ***** (max depth reached is examined therein)

% "Value" is not a dict.

lineify_value(Value,SettingsDict,mono(String)) :-
   integer(Value),
   !,
   get_setting(SettingsDict,spec_int,Placeholder,d),
   spec_format(Placeholder,Value,String).

lineify_value(Value,SettingsDict,mono(String)) :-
   float(Value),
   !,
   get_setting(SettingsDict,spec_float,Placeholder,f),
   spec_format(Placeholder,Value,String).
 
lineify_value(Value,_SettingsDict,mono(String)) :-
   string(Value),
   !,
   format(string(String),"~s",[Value]). % Won't have quotes even if there is whitespace inside

lineify_value(Value,_SettingsDict,mono(String)) :-
   atom(Value),
   !,
   format(string(String),"~a",[Value]). % Won't have quotes even if there is whitespace inside

lineify_value(Value,_SettingsDict,mono(String)) :-
   format(string(String),"~q",[Value]). % By default, quote

% ----------

% Print 'Value' according to 'Placeholder' (as opposed to, "according to
% a hardcoded format string"), yielding 'String'. We are using "safe_format/3"
% in case there is a problem with the format string. Then an exception message
% will appear in the output, but no exception will be thrown.

spec_format(Placeholder,Value,String) :-
   format(string(Format),"~~~s",[Placeholder]),
   safe_format(Format,[Value],String).

